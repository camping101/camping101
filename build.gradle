buildscript {
	dependencies {
		classpath "io.spring.gradle:dependency-management-plugin:1.0.10.RELEASE"
	}
	ext {
		queryDslVersion = "5.0.0"
		set('springCloudVersion', "2021.0.5")
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.9'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'com.ewerk.gradle.plugins.querydsl' version '1.0.10'
}

group = 'camping101'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	querydsl.extendsFrom compileClasspath
}

repositories {
	mavenCentral()
}

def querydslDir = "$buildDir/generated/querydsl"

querydsl {
	jpa = true
	querydslSourcesDir = querydslDir
}

sourceSets {
	main.java.srcDir querydslDir
}

compileQuerydsl {
	options.annotationProcessorPath = configurations.querydsl
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

dependencies {

	// Web MVC 관련
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.data:spring-data-envers'
	implementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1' // jwt
	implementation group: 'org.apache.commons',name: 'commons-collections4',version: '4.3' // jwt support
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
	implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

	// RDB / NoSQL
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.mariadb.jdbc:mariadb-java-client'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'   // jpa
	implementation 'org.springframework.boot:spring-boot-starter-data-redis' // redis
	runtimeOnly 'com.h2database:h2' // 테스트용

	// p6spy
	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.8.0'

	// QueryDSL
	implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
	annotationProcessor "com.querydsl:querydsl-apt:${queryDslVersion}"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation('it.ozimov:embedded-redis:0.7.3'){
		exclude group: 'org.slf4j', module: 'slf4j-simple' // slf4j 중복 방지용
	}

	// Swagger
	implementation group: 'io.springfox', name: 'springfox-boot-starter', version: '3.0.0'
	implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: '3.0.0'

	// ETC
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.4' // Jasypt : 보완키 암호화
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.apache.commons:commons-lang3:3.12.0'
	implementation 'commons-codec:commons-codec:1.15'

}

test {
	useJUnitPlatform()
	systemProperty "jasypt.encryptor.password", project.getProperties().get("jasypt.encryptor.password")
}

clean {
	delete querydslDir
}